# Sieve Script by Alice Bevan-McGregor <alice@gothcandy.com>
# For server-side filtering of mailing lists, bulk traffic, self-BCCs, and vacation time.
# Spam is dealt with by Amavis and an extensive list of plugins automatically.
#
# Excessive use of elsif to remove the need for the 'stop' statement.
#
# Note: Should use address :domain :is instead of address: contains.

require ["fileinto", "include", "imapflags", "regex", "relational", "comparator-i;ascii-numeric", "envelope", "subaddress"];


# Blacklist e-mail addresses.  TBD.
# if header :contains "from" [] {
#         discard;
#         stop;
# }


# Filter OEM supplier messages.
if address :domain :is "From" ["ncix.com", "ncix.net", "pecanada.com", "semicom.ca"] {
        fileinto "Suppliers";

# Filter ISPs and hosting providers.
} elsif address :domain :is "From" ["amazon.com", "zoneedit.com", "domainsatcost.ca", "rackspace.com", "slicehost.com", "webfaction.com"] {
        fileinto "Hosting";

# Filter music production resources.
} elsif address :domain :is "From" "tunecore.com" {
        fileinto "Projects.Music Production";

# Apple sends receipts for the "purchase" of free items.
} elsif allof ( address :contains "From" "do_not_reply@apple.com",
                header :contains "Subject" "Your receipt" ) {
        fileinto "Receipts";

# Filter + notation sub-addressing.
} elsif envelope :detail "to" "mvp" {
        fileinto "Projects.MVP";

# Mailing list traffic filters.
} elsif header :contains "List-Id" "turbogears" {
        fileinto "Projects.TurboGears";

# We're not polite to people who don't stop e-mailing us crap.
} elsif address "From" ["@klout.com"] {
        reject "Get knackered."

# I BCC my replies for better archiving, but I wrote it, so I don't need to read it.
} elsif address "Return-Path" ["alice@gothcandy.com", "amcgregor@topfloor.ca", "alice@matchfwd.com", "alice.mcgregor@me.com"] {
        setflag "\\seen";

} else {
        keep;
}
